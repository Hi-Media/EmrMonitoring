#!/usr/bin/gnuplot
#
# Copyright (c) 2013 Hi-Media SA
# Copyright (c) 2013 Geoffroy Aubry <gaubry@hi-media.com>
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software distributed under the License is distributed
# on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License
# for the specific language governing permissions and limitations under the License.
#

set term pngcairo size 1200,800 enhanced
set termoption dashed
set encoding utf8
set output output
set title sprintf("{/*2 Task timeline of “%s” jobflow}\n\n%s  |  %s  |  %s\nOther parameters: %s", name, jobflowid, size, instances, otherparameters) font ",9"
set grid xtics ytics
set xlabel "Elapsed time"
set ylabel "Running tasks" offset 1.0,0.0
set ytics nomirror
set xdata time
set format x "%Hh%M"
set timefmt "%s"
set style fill transparent solid 0.5 border
set key box
set xrange ["0":maxts]
set yrange ["0":maxnbtasks]

plot \
    csv using 1:2 title "maps" with filledcurves y1=0 fill lt 1 lc rgb "#9400D3" axis x1y1, \
    '' using 1:3 title "shuffle" with filledcurves y1=0 fill lt 1 lc rgb "#009E73" axis x1y1, \
    '' using 1:4 title "merge" with filledcurves y1=0 fill lt 1 lc 3 axis x1y1, \
    '' using 1:5 title "reduce" with filledcurves y1=0 fill lt 1 lc rgb "#F0E442" axis x1y1, \
    '' using 1:2 notitle with lines lt 1 lw 1 lc rgb "#9400D3" axis x1y1, \
    '' using 1:3 notitle with lines lt 1 lw 1 lc rgb "#009E73" axis x1y1, \
    '' using 1:5 notitle with lines lt 1 lw 1 lc rgb "#F0E442" axis x1y1, \
    '' using 1:4 notitle with lines lt 1 lw 1 lc 3 axis x1y1